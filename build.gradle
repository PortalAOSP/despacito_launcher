buildscript {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-alpha5'
    }
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.0'

    signingConfigs {
        travis {
            storeFile file("debug.jks")
            storePassword System.getenv("STORE_PASSWORD")
            keyAlias "Despacito Launcher"
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 26
        applicationId 'sos.aperture.despacito'
        buildConfigField "String", "MOBILE_CENTER_KEY", "\"" + System.getenv("MOBILE_CENTER_KEY") + "\""
        if (System.getenv("TRAVIS") == "true") {
            versionCode = Integer.valueOf(System.getenv("TRAVIS_BUILD_NUMBER"))
            versionName = "Travis-" + System.getenv("TRAVIS_BUILD_NUMBER")
        } else {
            versionCode 1
            versionName "prerelease"
        }

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            if (System.getenv("TRAVIS") == "true") {
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.flags'
                if (System.getenv("TRAVIS_EVENT_TYPE") != "pull_request") {
                    signingConfig signingConfigs.travis
                }
            } else {
                minifyEnabled false
            }
        }
    }

    flavorDimensions "variant"

    productFlavors {
        aosp {
            applicationId 'sos.aperture.despacito'
            testApplicationId 'sos.aperture.despacito.tests'
            dimension "variant"
        }
    }
    sourceSets {
        main {
            res.srcDirs = ['res']
            java.srcDirs = ['src', 'src_config']
            manifest.srcFile "AndroidManifest.xml"
        }
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    mavenCentral()
    google()
    jcenter()
}

dependencies {
    def mobileCenterSdkVersion = '0.10.0'
    def supportLibVersion = '26.0.0-beta2'
    compile "com.microsoft.azure.mobile:mobile-center-analytics:${mobileCenterSdkVersion}"
    compile "com.microsoft.azure.mobile:mobile-center-crashes:${mobileCenterSdkVersion}"
    compile "com.microsoft.azure.mobile:mobile-center-distribute:${mobileCenterSdkVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:palette-v7:${supportLibVersion}"
}
